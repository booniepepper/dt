#!/usr/bin/env dt

# Don't expose any commands defined later in the file.
defs \FROZEN_DEFS :

[quote \do doin] \eval def

# TODO: Why don't multiline strings work?
[ "<!doctype html>"
  "<html>"
  "<head>"
    "<title>dt: standard library</title>"
    "<link rel='stylesheet' href='//cdn.jsdelivr.net/npm/hack-font@3/build/web/hack.css'>"
    "<link rel='stylesheet' href='style.css'>"
  "</head>"
  "<body>"
    "<div id='wide-strip'>"
    "<header><center><a href='index.html'>"
      "<h1 id='logo'>dt</h1>"
      "<h2><pre>duct tape for your unix pipes</pre></h2>"
    "</a></center></header>"
    "<main>"
      "<hr>"
      ["<center><h2>dt " version " standard library</h2></center>"] eval "" join
      "<table id='stdlib'>"
] eval

[ "<" split "&lt;" join
  ">" split "&gt;" join
] \html-escape def

[[cmd]:
  "<tr>"
    "<td class='lighter'>" cmd "</td>"
    "<td>" cmd usage html-escape "</td>"
  "</tr>"
] \cmd-row def

FROZEN_DEFS
  ["dt/" starts-with? not] filter
  [\cmd-row doin   "" join] map
  "\n  " join
  push

[ "</table>"
  "</main>"
  "</div>"
  "</body>"
  "</html>"
] concat

"\n" join

.s

\html :
  html "stdlib.html" writef
  html ["archive/stdlib-"] version push ".html" push "" join writef

